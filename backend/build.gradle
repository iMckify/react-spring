plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.imckify'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    spockVersion = '2.4-M1-groovy-4.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation(
            'org.springframework.boot:spring-boot-starter-test',
            "org.spockframework:spock-core:${spockVersion}",
            "org.spockframework:spock-spring:${spockVersion}",
    )
}

tasks.jar.enabled = false

test {
    useJUnitPlatform()
}

tasks.register('addFrontendToBackend', Copy) {
    dependsOn(':frontend:assemble')
    from "$rootDir/frontend/build"
    into "$buildDir/resources/main/static/."
}

processResources.dependsOn addFrontendToBackend

